plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.devtools.ksp'
}

def version = 4
def is_debug = project.gradle.startParameter.taskNames.any {
    name -> name.toLowerCase().contains("debug")
}

def gitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def versionString = { ->
    if (is_debug) {
        return "1.$version (${gitHash()})"
    } else {
        return "1.$version"
    }
}

android {
    compileSdk 34

    defaultConfig {
        applicationId "dubrowgn.microtimer"
        minSdk 28 // Handler.postDelayed()
        targetSdk 34
        versionCode version
        versionName versionString()
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    namespace 'dubrowgn.microtimer'
}

dependencies {
    def room_version = "2.6.1"
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    ksp "androidx.room:room-compiler:$room_version"

    implementation 'androidx.core:core-ktx:1.12.0'
}